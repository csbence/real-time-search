group 'unh-ai'
version '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: 'excelsiorJet'
apply plugin: 'kotlinx-serialization'

applicationDefaultJvmArgs = [
        "-server", "-XX:+AggressiveOpts", "-ea", //"â€“XX:+UseG1GC",
        "-Xms10g",
        "-Xmx10g"
]

if (project.hasProperty("mainClass")) {
    mainClassName = mainClass
} else {
    mainClassName = "edu.unh.cs.ai.realtimesearch.RealTimeSearchKt"
}

buildscript {
    ext.kotlin_version = '1.2.30'
    ext.ktor_version = '0.9.1'

    repositories {
        mavenCentral()
//        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
        maven { url "https://kotlin.bintray.com/kotlinx" }

    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$serialization_version"
        classpath 'com.excelsiorjet:excelsior-jet-gradle-plugin:1.1.2'
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
    jcenter()

    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "http://jcenter.bintray.com/"}
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

    compile 'commons-cli:commons-cli:1.3.1'

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-nop:1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += file('src/main/kotlin')
    test.java.srcDirs += file('src/test/kotlin')
}

task plot(type: Exec) {
    workingDir 'scripts'
    commandLine 'python3', 'metronome_plot.py'
}

run {
    if (project.hasProperty("appArgs")) {
        args = Eval.me(appArgs)
    }
    standardInput = System.in
}

jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

excelsiorJet {
    mainClass = mainClassName
}

task opticProcessor(type: JavaExec) {
    main = "edu.unh.cs.ai.realtimesearch.OpticMetaProcessorKt"
    classpath = sourceSets.main.runtimeClasspath
}

task opticProcessorJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'edu.unh.cs.ai.realtimesearch.OpticMetaProcessorKt'
    }
    baseName = project.name + '-optic-processor'

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task clientJar(type: Jar) {
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes 'Main-Class': 'edu.unh.cs.ai.realtimesearch.experiment.network.RealTimeSearchClientApplicationKt'
    }
    baseName = project.name + '-client'


    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}